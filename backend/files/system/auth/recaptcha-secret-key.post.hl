
// Sets the reCAPTCHA key and secret to the specified values.
.arguments
   key:string
   secret:string
.description:Sets the reCAPTCHA key and secret to the specified values
.type:internal

// Making sure user is allowed to invoke endpoint
auth.ticket.verify:root

// Sanity checking invocation.
validators.mandatory:x:@.arguments/*/key
validators.mandatory:x:@.arguments/*/secret

// Updating settings.
config.load
json2lambda:x:-
if
   not-exists:x:@json2lambda/*/magic/*/auth/*/recaptcha
   .lambda
      add:x:@json2lambda/*/magic/*/auth
         .
            recaptcha
               key
               secret
set-value:x:@json2lambda/*/magic/*/auth/*/recaptcha/*/key
   get-value:x:@.arguments/*/key
set-value:x:@json2lambda/*/magic/*/auth/*/recaptcha/*/secret
   get-value:x:@.arguments/*/secret

// Saving updated settings.
lambda2json:x:@json2lambda/*
config.save:x:-

// Retrieving key configuration setting and returning to caller.
return-nodes
   result:success
