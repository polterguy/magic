
/*
 * Changes the password of the currently authenticated user.
 * 
 * Requires the user to be successfully authenticated, and an [.arguments]/[password]
 * being the new password to use for authenticated user.
 */
slots.create:magic.auth.change-password

   // Ensuring a password was actually provided.
   validators.mandatory:x:@.arguments/*/password

   /*
    * Retrieving currently authenticated user's username, and sanity checking to make sure
    * client actually is authenticated.
    */
   auth.ticket.get
   if
      null:x:@auth.ticket.get
      .lambda

         // Oops ...!!
         throw:Access denied
            status:int:401
            public:bool:true

   // Hashing password.
   .password
   set-value:x:@.password
      crypto.password.hash:x:@.arguments/*/password

   // Opens up our database connection.
   data.connect:magic
      database-type:sqlite

      /*
       * Changes the password of the currently logged in user
       * to whatever new password user provided.
       */
      data.update
         table:users
         values
            password:x:@.password
         where
            and
               username.eq:x:@auth.ticket.get
