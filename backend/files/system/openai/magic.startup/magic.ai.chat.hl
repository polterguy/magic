
/*
 * Chat slot for having conversations with OpenAI's "gpt" type of models.
 */
slots.create:magic.ai.chat

   // Retrieving relevant snippets.
   unwrap:x:+/*
   signal:magic.ai.get-context
      type:x:@.arguments/*/type
      prompt:x:@.arguments/*/prompt
      threshold:x:@.arguments/*/threshold
      max_tokens:x:@.arguments/*/max_context_tokens
      vector_model:x:@.arguments/*/vector_model
      api_key:x:@.arguments/*/api_key

   // Checking if we've got a cached result.
   if
      exists:x:@signal/*/cached
      .lambda

         // Cached result, checking if type is "supervised".
         if
            and
               exists:x:@.arguments/*/supervised
               not-null:x:@.arguments/*/supervised
               eq
                  convert:x:@.arguments/*/supervised
                     type:int
                  .:int:1
            .lambda

               // Storing response to ml_requests to keep history.
               data.connect:[generic|magic]
                  data.create
                     table:ml_requests
                     values
                        type:x:@.arguments/*/type
                        prompt:x:@.arguments/*/prompt
                        completion:x:@signal/*/cached
                        finish_reason:cached
                        session:x:@.arguments/*/session

         // Returning cached result to caller.
         unwrap:x:+/*
         return
            result:x:@signal/*/cached
            finish_reason:cached

   // Taken from either training data or previous questions.
   .session

   // Checking if question triggered training data.
   if
      exists:x:@signal/*/context
      .lambda

         /*
          * Question triggered training data.
          *
          * Checking if we've got a [system_message] value.
          */
         if
            and
               not-null:x:@.arguments/*/system_message
               neq:x:@.arguments/*/system_message
                  .:
            .lambda

               // Adding system message to [.session]
               unwrap:x:+/*/*/*/content
               add:x:@.session
                  .
                     .
                        role:system
                        content:x:@.arguments/*/system_message

         // Then adding context to [.session]
         unwrap:x:+/*/*/*/content
         add:x:@.session
            .
               .
                  role:system
                  content:x:@signal/*/context

   else-if
      and
         exists:x:@.arguments/*/session
         not-null:x:@.arguments/*/session
      .lambda

         // Question didn't kick in training data, hence checking if we've got a session.
         cache.get:x:@.arguments/*/session
         if
            not-null:x:@cache.get
            .lambda

               /*
                * We have an existing session, hence continuing discussing subject
                * unless embeddings returns a better match.
                */
               add:x:@.session
                  hyper2lambda:x:@cache.get

         else-if
            and
               not-null:x:@.arguments/*/system_message
               neq:x:@.arguments/*/system_message
                  .:
            .lambda

               // No training data and no session, but we do have a system message.
               unwrap:x:+/*/*/*/content
               add:x:@.session
                  .
                     .
                        role:system
                        content:x:@.arguments/*/system_message

   // Checking if model is prefixed, at which point we prepend prefix to question.
   .only-prompt
   set-value:x:@.only-prompt
      get-value:x:@.arguments/*/prompt
   if
      and
         exists:x:@.arguments/*/prefix
         not-null:x:@.arguments/*/prefix
         neq:x:@.arguments/*/prefix
            .:
      .lambda

         // Prefixing prompt.
         set-value:x:@.arguments/*/prompt
            strings.concat
               get-value:x:@.arguments/*/prefix
               get-value:x:@.arguments/*/prompt

   // Adding user's current question to session.
   unwrap:x:+/*/*/*/content
   add:x:@.session
      .
         .
            role:user
            content:x:@.arguments/*/prompt

   /*
    * Pruning previous messages until size of context + max_tokens is less than whatever
    * amount of tokens the model can handle.
    *
    * Notice, we never submit more than 4 previous messages to OpenAI as context.
    * We also make sure we always submit the FIRST message if existing, since this
    * is the context fetched from our training data from the first question.
    */
   .cont:bool:true
   while:x:@.cont

      lambda2hyper:x:@.session/*
      if
         and
            lt
               get-count:x:@.session/*
               .:int:5
            lt
               math.add
                  get-value:x:@.arguments/*/max_tokens
                  openai.tokenize:x:@lambda2hyper
               get-value:x:@.arguments/*/model_size
         .lambda

            // Context is small enough to answer according to max_tokens.
            set-value:x:@.cont
               .:bool:false
      else

         /*
          * Need to remove one of our previous messages to be able to have OpenAI return max_tokens.
          * Notice, we make sure we always keep the first two messages, since it's the most important
          * part that was created when we initially found our context.
          *
          * The first message is possibly the system message, while the second message is the context.
          */
         remove-nodes:x:@.session/2

   // Retrieving token used to invoke OpenAI.
   .token
   set-value:x:@.token
      strings.concat
         .:"Bearer "
         get-first-value
            get-value:x:@.arguments/*/api_key
            config.get:"magic:openai:key"

   // Invoking OpenAI now with context being either training data or previous messages.
   add:x:./*/http.post/*/payload/*/messages
      get-nodes:x:@.session/*
   http.post:"https://api.openai.com/v1/chat/completions"
      headers
         Authorization:x:@.token
         Content-Type:application/json
      payload
         model:x:@.arguments/*/model
         max_tokens:x:@.arguments/*/max_tokens
         temperature:x:@.arguments/*/temperature
         messages
      convert:true

   // Sanity checking above invocation
   if
      not
         and
            mte:x:@http.post
               .:int:200
            lt:x:@http.post
               .:int:300
      .lambda

         // Oops, error - Logging error and returning status 500 to caller.
         lambda2hyper:x:@http.post
         log.error:Something went wrong while invoking OpenAI
            message:x:@http.post/*/content/*/error/*/message
            status:x:@http.post
            error:x:@lambda2hyper
         throw:x:@http.post/*/content/*/error/*/message
            public:bool:true
            status:x:@http.post

   else

      // Success!
      log.info:Invoking OpenAI was a success

   // Making sure we trim response.
   .result
   set-value:x:@.result
      strings.trim:x:@http.post/*/content/*/choices/0/*/message/*/content
   get-first-value
      get-value:x:@http.post/*/content/*/choices/0/*/finish_reason
      .:unknown

   // Adding current prompt to session, but only if caller provided a session.
   if
      and
         exists:x:@.arguments/*/session
         not-null:x:@.arguments/*/session
      .lambda

         // Caller provided a session.
         unwrap:x:+/*/*/*/content
         add:x:@.session
            .
               .
                  role:system
                  content:x:@.result
         lambda2hyper:x:@.session/*
         cache.set:x:@.arguments/*/session
            expiration:600
            value:x:@lambda2hyper

   // Opening database connection to store request into ml_requests table.
   data.connect:[generic|magic]

      // Checking if type is "supervised".
      if
         and
            exists:x:@.arguments/*/supervised
            not-null:x:@.arguments/*/supervised
            eq
               convert:x:@.arguments/*/supervised
                  type:int
               .:int:1
         .lambda

            // Storing response to ml_requests to keep history.
            data.create
               table:ml_requests
               values
                  type:x:@.arguments/*/type
                  prompt:x:@.only-prompt
                  completion:x:@.result
                  finish_reason:x:@get-first-value
                  session:x:@.arguments/*/session

      // Checking if we've got references.
      if
         and
            exists:x:@.arguments/*/references
            not-null:x:@.arguments/*/references
            get-value:x:@.arguments/*/references
            exists:x:@signal/*/snippets
         .lambda
            add:x:../*/return
               .
                  references
            add:x:../*/return/*/references
               get-nodes:x:@signal/*/snippets/*

   // Checking if we've configured integrations for outgoing messages.
   .outgoing
   set-value:x:@.outgoing
      get-first-value
         get-value:x:@.arguments/*/webhook_outgoing
         config.get:"magic:openai:integrations:outgoing:slot"
   if
      and
         not-null:x:@.outgoing
         neq:x:@.outgoing
            .:
      .lambda

         // Invoking integration slot.
         .exe

            // Retrieving URL to invoke.
            .hook-url
            set-value:x:@.hook-url
               get-first-value
                  get-value:x:@.arguments/*/webhook_outgoing_url
                  config.get:"magic:openai:integrations:outgoing:url"

            // Invoking slot.
            unwrap:x:./*/signal/*/url
            signal:x:@.outgoing
               url:x:@.hook-url

         // Parametrizing invocation to integration slot.
         unwrap:x:+/*/*
         add:x:@.exe/*/signal
            .
               result:x:@.result
               session:x:@.arguments/*/session

         // Parametrizing invocation to integration slot.
         if
            and
               exists:x:@.arguments/*/to
               exists:x:@.arguments/*/from
               not-null:x:@.arguments/*/to
               not-null:x:@.arguments/*/from
               strings.contains:x:@.arguments/*/to
                  .:":"
               strings.contains:x:@.arguments/*/from
                  .:":"
            .lambda

               // We have a channel to accommodate for.
               .channel
               .to
               .from
               strings.split:x:@.arguments/*/to
                  .:":"
               set-value:x:@.channel
                  get-value:x:@strings.split/0
               set-value:x:@.to
                  get-value:x:@strings.split/1
               strings.split:x:@.arguments/*/from
                  .:":"
               set-value:x:@.from
                  get-value:x:@strings.split/1
               unwrap:x:+/*/*
               add:x:@.exe/*/signal
                  .
                     to:x:@.from
                     from:x:@.to
                     channel:x:@.channel

         else

            // No channel
            add:x:@.exe/*/signal
               get-nodes:x:@.arguments/*/to
               get-nodes:x:@.arguments/*/from

         // Invoking callback.
         eval:x:@.exe

   // Returning results returned from invocation above to caller.
   unwrap:x:+/*
   return
      result:x:@.result
      finish_reason:x:@get-first-value
      db_time:x:@signal/*/db_time
