
/*
 * Crawls the specified website generating training data for machine learning in the process.
 */
slots.create:magic.ai.crawl-site

   // Creating a thread and invoking file doing the heavy lifting.
   insert-before:x:./*/fork/0
      get-nodes:x:@.arguments
   fork

      // Making sure exceptions does not leave thread.
      try

         // Invoking slot responsible for doing the actual crawling.
         add:x:./*/signal
            get-nodes:x:@.arguments/*
         signal:magic.ai.crawl-site-on-thread

      .catch

         // Oops ...!!
         log.error:x:@.arguments/*/message
            url:x:@.arguments/*/url

         // Signaling frontend.
         strings.concat
            .:"Error - "
            get-value:x:@.arguments/*/message
         unwrap:x:+/*/args/*
         sockets.signal:x:@.arguments/@.arguments/*/feedback-channel
            roles:root
            args
               message:x:@strings.concat
               type:error
         sleep:100

         // Checking if caller supplied [.onerror] handler.
         if
            exists:x:@.onerror
            .lambda
               add:x:+/+
                  get-nodes:x:@.arguments/*
               add:x:+
                  get-nodes:x:@.arguments/@.arguments/*
               invoke:x:@.onerror
