
/*
 * Creates scheduled task that deletes all cached questions / answers
 * from machine learning models that are older than 2 weeks, to preserve
 * storage space.
 *
 * Notice, task ONLY executes if default database is SQLite, since database
 * management is up to db-admin for serious databases.
 */
if
   eq
      config.get:"magic:databases:default"
      .:sqlite
   .lambda

      // Wrapping in try/catch in case task already exists.
      try

         // Deleting old task if existing.
         tasks.list:delete-old-machine-learning-requests
         if
            exists:x:@tasks.list/*
            .lambda
               tasks.delete:x:@tasks.list/0/*/id

         // Creating task that periodically deletes old historical requests.
         tasks.create:delete-old-machine-learning-requests
            description:Periodically deletes all machine learning requests older than 14 days
            repeats:72.hours
            .lambda
               sqlite.connect:magic
                  sqlite.execute:"delete from ml_requests where created < date('now', '-14 day') and cached != 1 and questionnaire != 1"
               log.info:Task to cleanup old machine learning requests executed successfully
                  deleted:x:@sqlite.connect/*/sqlite.execute

      .catch

         // Task already exists.
         console.log:Task to periodically delete historical requests already exists
