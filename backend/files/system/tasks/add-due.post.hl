
// Schedules the task with the specified [id] for execution according to the specified parameters.
.arguments
   id:string
   due:date
   repeats:string
.description:Schedules the task with the specified id for execution

// Ensures user is authorized to access endpoint.
auth.ticket.verify:root

/*
 * Adds either a [repeats] or [due] argument to invocation below,
 * depending upon what argument was supplied by caller.
 */
if
   not
      eq
         get-value:x:@.arguments/*/repeats
         .
   .lambda
      add:x:../*/tasks.schedule
         get-nodes:x:@.arguments/*/repeats
else
   add:x:../*/tasks.schedule
      get-nodes:x:@.arguments/*/due

// Performs actual scheduling of task.
tasks.schedule:x:@.arguments/*/id

// Returns success to caller.
return-nodes
   result:success
