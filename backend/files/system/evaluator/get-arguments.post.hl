
/*
 * Returns arguments the specified Hyperlambda require if any.
 */
.arguments
   hyperlambda:string
.description:Returns arguments the specified Hyperlambda require if any
.type:internal

// Ensures user is authorized to access endpoint.
auth.ticket.verify:root

// Transforming Hyperlambda to lambda object.
hyper2lambda:x:@.arguments/*/hyperlambda

// Making sure we "normalize" arguments.
for-each:x:@hyper2lambda/*/.arguments/*
   if
      and
         not-exists:x:@.dp/#/*/type
         not-null:x:@.dp/#
      .lambda
         unwrap:x:+/*/*
         add:x:@.dp/#
            .
               type:x:@.dp/#
         set-value:x:@.dp/#

// Returning arguments Hyperlambda requires to caller.
return-nodes:x:@hyper2lambda/*/.arguments/*
