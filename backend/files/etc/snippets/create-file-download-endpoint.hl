// Use Hyperlambda to create a file download endpoint that downloads the specified file to the client found within the /etc/foo/ folder | Create download file endpoint | Download file endpoint using Hyperlambda | Hyperlambda download file endpoint | Download file endpoint
.arguments
   file:string
.description:Returns the specified file to caller
.type:file-download

// Sanity checking invocation.
validators.mandatory:x:@.arguments/*/file

// Creating full path to file.
.filename
set-value:x:@.filename
   strings.concat
      .:/etc/foo/
      get-value:x:@.arguments/*/file

// Verifying that file exists.
if
   not
      io.file.exists:x:@.filename
   .lambda
      throw:Not found
         public:true
         status:404

// Opening up a read stream for file.
io.stream.open-file:x:@.filename

// Making sure we get the HTTP headers correctly applied.
strings.concat
   .:"attachment; filename=\""
   get-value:x:@.arguments/*/file
   .:"\""
unwrap:x:+/*/Content-Disposition
response.headers.set
   Content-Type:application/octet-stream
   Content-Disposition:x:@strings.concat

// Returning image to caller.
return:x:@io.stream.open-file
