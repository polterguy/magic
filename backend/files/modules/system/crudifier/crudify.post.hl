
/*
 * Creates an HTTP REST CRUD endpoint, wrapping your database and table according to the specified arguments.
 */
.arguments
   verbose:bool
   databaseType:string
   moduleName:string
   database:string
   table:string
   moduleUrl:string
   returnId:bool
   template:string
   verb:string
   auth:string
   log:string
   overwrite:bool
   validators:string
   cache:int
   publicCache:bool
   cqrs:bool
   cqrsAuthorisation:string
   cqrsAuthorisationValues:string
   args:*
   conditions:*
.description:Creates an HTTP REST CRUD endpoint, wrapping your database and table according to the specified arguments

/*
 * Sanity checks invocation.
 */
validators.mandatory:x:@.arguments/*/moduleName
validators.mandatory:x:@.arguments/*/database
validators.mandatory:x:@.arguments/*/table
validators.mandatory:x:@.arguments/*/moduleUrl
validators.mandatory:x:@.arguments/*/template
validators.mandatory:x:@.arguments/*/verb
validators.enum:x:@.arguments/*/databaseType
   .:mysql
   .:mssql
validators.enum:x:@.arguments/*/verb
   .:post
   .:get
   .:put
   .:delete
validators.enum:x:@.arguments/*/template
   .:/modules/system/crudifier/templates/crud.template.post.hl
   .:/modules/system/crudifier/templates/crud.template.get.hl
   .:/modules/system/crudifier/templates/crud.template.put.hl
   .:/modules/system/crudifier/templates/crud.template.delete.hl
validators.enum:x:@.arguments/*/cqrsAuthorisation
   .:inherited
   .:roles
   .:groups
   .:users
   .:none

/*
 * Defaulting [databaseType] to default database type from config.
 */
if
   not
      exists:x:@.arguments/*/databaseType
   .lambda

      /*
       * Just inserting value from config into [.arguments] for simplicity.
       */
      config.get:"magic:databases:default"
      unwrap:x:+/*/*
      add:x:@.arguments
         .
            databaseType:x:@config.get

/*
 * Ensures user is authorized to access endpoint.
 */
auth.ticket.verify:root

/*
 * Synchronising access to endpoint to avoid race condition.
 */
semaphore:crudifier

   /*
    * Ensures user is authorized to access endpoint.
    */
   auth.ticket.verify:root

   /*
    * Sanity checking module name, to prevent overwriting "/system/".
    */
   strings.to-lower:x:@.arguments/*/moduleName
   if
      eq
         get-value:x:@strings.to-lower
         .:system
      .lambda
         throw:"You can't use the name 'system' as your module's name"
            public:true
            status:401

   /*
    * Sanity checking module name, to make sure it has a name at all.
    */
   if
      or
         eq
            get-value:x:@strings.to-lower
            .:
         eq
            get-value:x:@strings.to-lower
            .
      .lambda
         throw:"You can't use an empty string as your module's name. Your module must have a name."
            public:true
            status:401

   /*
    * Creating a log entry explaining roughly what we're about to do.
    */
   log.info
      .:"Crudifying '"
      get-value:x:@strings.to-lower
      .:"' wrapping '"
      get-value:x:@.arguments/*/database
      .:"' and '"
      get-value:x:@.arguments/*/table
      .:"' with verb '"
      get-value:x:@.arguments/*/verb
      .:"'"

   /*
    * Making sure we create a valid filename.
    */
   .filename
   set-value:x:-
      strings.replace:x:@.arguments/*/moduleUrl
         .:'.'
         .:/
   set-value:x:@.filename
      strings.to-lower:x:@.filename

   /*
    * Creating our endpoint according to arguments given, by using [apply]
    * on the [template] file given.
    */
   io.file.load:x:@.arguments/*/template
   hyper2lambda:x:-
   unwrap:x:+/*
   apply:x:@hyper2lambda
      database-type:x:@.arguments/*/databaseType
      database:x:@.arguments/*/database
      table:x:@.arguments/*/table
      return-id:x:@.arguments/*/returnId
      args

   /*
    * Then ensuring we create a log entry during invocation of endpoint,
    * if the caller wants us to do such a thing.
    */
   if
      and
         exists:x:@.arguments/*/log
         not
            eq
               get-value:x:@.arguments/*/log
               .:
      .lambda
         unwrap:x:+/*/*/0
         insert-after:x:@apply/*/.type
            .
               log.info
                  .:x:@.arguments/*/log
                  auth.ticket.get
                  .:"\r\n\r\n"
                  lambda2hyper:x:@.arguments/*

   /*
    * Making sure we append validators, if given by client.
    *
    * Notice, we only do this for PUT or POST verbs.
    */
   if
      and
         exists:x:@.arguments/*/validators
         not
            eq
               get-value:x:@.arguments/*/validators
               .:
         or
            eq
               get-value:x:@.arguments/*/verb
               .:post
            eq
               get-value:x:@.arguments/*/verb
               .:put
      .lambda
         insert-after:x:@apply/*/.type
            hyper2lambda:x:@.arguments/*/validators

   /*
    * Then ensuring we turn on authorization to our endpoint according
    * to caller's specifications, if caller supplied an [auth] value.
    */
   if
      exists:x:@.arguments/*/auth
      .lambda

         /*
          * Checking if '*' was provided as the value for authentication,
          * at which point we simply add authentication, without any
          * roles - Ensuring only authenticated users can access endpoint,
          * but no requirements in regards to roles are specified.
          */
         if
            eq
               get-value:x:@.arguments/*/auth
               .:*
            .lambda

               /*
                * No roles specified, but user must be authenticated.
                */
               insert-after:x:@apply/*/.type
                  .
                     auth.ticket.verify

         else

            /*
             * Comma separated list of roles specified.
             */
            unwrap:x:+/*/*
            insert-after:x:@apply/*/.type
               .
                  auth.ticket.verify:x:@.arguments/*/auth

   /*
    * Then making sure we do our specialized transformation,
    * according to verb requested by caller.
    */
   switch:x:@.arguments/*/verb


      /*
       * HTTP DELETE special case
       */
      case:delete

         /*
          * Sanity checking invocation.
          */
         if
            eq
               get-count:x:@.arguments/*/args/*/primary/*
               .:int:0
            .lambda


               /*
                * Cannot have a DELETE endpoint without primary keys.
                */
               throw:There are no actual primary keys declared for your DELETE endpoint.
                  public:true
                  status:401

         /*
          * Applying DELETE arguments.
          *
          * This basically ensures that all primary keys becomes mandatory arguments.
          */
         add:x:@apply/*/.arguments
            get-nodes:x:@.arguments/*/args/*/primary/*/*
         for-each:x:@.arguments/*/args/*/primary/*/*
            .x:@.arguments/*/
            set-value:x:@.x
               strings.concat
                  get-value:x:@.x
                  get-name:x:@.dp/#
            set-x:x:+/*/*
               convert:x:@.x
                  type:x
            insert-after:x:@apply/*/.type
               .
                  validators.mandatory

      /*
       * HTTP GET special case
       */
      case:get

         /*
          * Sanity checking invocation.
          */
         if
            eq
               get-count:x:@.arguments/*/args/*/columns/*
               .:int:0
            .lambda

               /*
                * Cannot have a GET endpoint returning no actual columns.
                */
               throw:There are no actual columns declared for your GET endpoint.
                  public:true
                  status:401

         /*
          * Checking if we have an [.arguments]/[args]/[cache] value.
          */
         if
            and
               exists:x:@.arguments/*/cache
               not
                  eq
                     get-value:x:@.arguments/*/cache
                     .:
               not
                  eq
                     get-value:x:@.arguments/*/cache
                     .:0
            .lambda

               /*
                * Caller wants results of endpoint to be cached, figuring out
                * if we should publicly or privately cache the result.
                */
               .scope:private
               if
                  eq
                     get-value:x:@.arguments/*/publicCache
                     .:bool:true
                  .lambda
                     set-value:x:@.scope
                        .:public
               strings.concat
                  get-value:x:@.scope
                  .:, max-age=
                  get-value:x:@.arguments/*/cache
               unwrap:x:+/*/*/*
               insert-after:x:@apply/*/.type
                  .
                     response.headers.add
                        Cache-Control:x:@strings.concat

         /*
          * Checking if caller supplied an explicit conditions collection,
          * and if not, applying the default logic - If caller supplied an
          * explicit condition collection, we use that instead.
          */
         if
            exists:x:@.arguments/*/conditions
            .lambda

               /*
                * Applying custom conditions to endpoint.
                */
               add:x:@apply/*/.arguments
                  get-nodes:x:@.arguments/*/conditions/*/*

         else

            /*
             * Creating and applying default conditions to endpoint
             *
             * This is the part where we create our condition arguments, allowing
             * for filtering result when endpoint is invoked.
             *
             * Making sure we legalize all the most common operators for all
             * columns, in addition to "like" conditions for string types of
             * endpoints.
             */
            .operators
               mt
               lt
               mteq
               lteq
               neq
               eq

            /*
             * Checking if caller wants 'verbose' scaffolding.
             */
            if
               or
                  not
                     exists:x:@.arguments/*/verbose
                  not
                     get-value:x:@.arguments/*/verbose
               .lambda
                  remove-nodes:x:@.operators/*/mt
                  remove-nodes:x:@.operators/*/lt
                  remove-nodes:x:@.operators/*/mteq
                  remove-nodes:x:@.operators/*/lteq
                  remove-nodes:x:@.operators/*/neq

            /*
             * Applying arguments to endpoint.
             */
            for-each:x:@.arguments/*/args/*/columns/*


               /*
                * Checking if argument needs a ".like" comparison type,
                * which is true only for "string" type of arguments.
                */
               if
                  eq
                     get-value:x:@.dp/#/*/type
                     .:string
                  .lambda

                     /*
                      * String type of argument, appending ".like" to legal arguments.
                      */
                     .buffer
                        .name:x:@.dp/#/*/type
                     unwrap:x:@.buffer/*
                     set-name:x:@.buffer/*
                        strings.concat
                           get-value:x:@.dp/#/*/name
                           .:.like
                     add:x:@apply/*/.arguments
                        get-nodes:x:@.buffer/*

               /*
                * Checking if argument is boolean type of argument, at which
                * point we ONLY add ".eq" type of argument to it.
                */
               if
                  eq
                     get-value:x:@.dp/#/*/type
                     .:bool
                  .lambda

                     /*
                      * Boolean type of argument, removing everything but ".eq" type of arguments.
                      */
                     .buffer
                        .name:x:@.dp/#/*/type
                     unwrap:x:@.buffer/*
                     set-name:x:@.buffer/*
                        strings.concat
                           get-value:x:@.dp/#/*/name
                           .:.eq
                     add:x:@apply/*/.arguments
                        get-nodes:x:@.buffer/*

               else

                  /*
                   * Not a boolean type of argument, hence looping through all of
                   * our default comparison operators.
                   */
                  for-each:x:@.operators/*
                     .buffer
                        .name:x:@.dp/@.dp/#/*/type
                     unwrap:x:@.buffer/*
                     set-name:x:@.buffer/*
                        strings.concat
                           get-value:x:@.dp/@.dp/#/*/name
                           .:.
                           get-name:x:@.dp/#
                     add:x:@apply/*/.arguments
                        get-nodes:x:@.buffer/*

         /*
          * Making sure we explicitly declare which [columns] that our
          * endpoint should return, according to caller's specifications.
          */
         for-each:x:@.arguments/*/args/*/columns/*/*/name
            set-name:x:+/*/*
               get-value:x:@.dp/#
            add:x:@apply/**/columns
               .
                  .

         /*
          * Applying foreign keys if specified.
          */
         for-each:x:@.arguments/*/args/*/columns/*

            /*
             * Checking if currently iterated column has a foreign key declaration.
             */
            if
               exists:x:@.dp/#/*/foreign_key/*
               .lambda

                  /*
                   * Adding meta information about foreign key to lambda object.
                   */
                  unwrap:x:+/*/*/*
                  add:x:@apply/*/.foreign-keys
                     .
                        .
                           column:x:@.dp/#/*/name
                           table:x:@.dp/#/*/foreign_key/*/table
                           foreign_column:x:@.dp/#/*/foreign_key/*/column
                           foreign_name:x:@.dp/#/*/foreign_key/*/name


         /*
          * Checking if we have foreign keys at all, and if not, removing entire declaration.
          */
         if
            not
               exists:x:@apply/*/.foreign-keys/*
            .lambda

               /*
                * Removing [.foreign-key] declaration altogether.
                */
               remove-nodes:x:@apply/*/.foreign-keys

      /*
       * HTTP POST special cases
       */
      case:post

         /*
          * Sanity checking invocation.
          */
         if
            eq
               get-count:x:@.arguments/*/args/*/columns/*
               .:int:0
            .lambda

               /*
                * Cannot have a POST endpoint without any actual data to POST.
                */
               throw:There are no actual columns declared for your POST endpoint.
                  public:true
                  status:401

         /*
          * Applying POST arguments, which are basically the [values] that
          * invokers of endpoint are allowed to insert during creationg of
          * record.
          */
         for-each:x:@.arguments/*/args/*/columns/*
            set-name:x:+/*/*
               get-value:x:@.dp/#/*/name
            add:x:@apply/*/.arguments
               .
                  .

         /*
          * Checking if caller specified he wanted to have ID of item returned,
          * and if not, only returning success.
          */
         if
            not
               get-value:x:@.arguments/*/returnId
            .lambda
               remove-nodes:x:@apply/**/return/*
               add:x:@apply/**/return
                  .
                     result:success


         /*
          * Applying foreign keys if specified.
          */
         for-each:x:@.arguments/*/args/*/columns/*

            /*
             * Checking if currently iterated column has a foreign key declaration.
             */
            if
               exists:x:@.dp/#/*/foreign_key/*
               .lambda

                  /*
                   * Adding meta information about foreign key to lambda object.
                   */
                  unwrap:x:+/*/*/*
                  add:x:@apply/*/.foreign-keys
                     .
                        .
                           column:x:@.dp/#/*/name
                           table:x:@.dp/#/*/foreign_key/*/table
                           foreign_column:x:@.dp/#/*/foreign_key/*/column
                           foreign_name:x:@.dp/#/*/foreign_key/*/name

         /*
          * Checking if we have foreign keys at all, and if not, removing entire declaration.
          */
         if
            not
               exists:x:@apply/*/.foreign-keys/*
            .lambda

               /*
                * Removing [.foreign-key] declaration altogether.
                */
               remove-nodes:x:@apply/*/.foreign-keys

      /*
       * HTTP PUT special cases
       */
      case:put

         /*
          * Sanity checking invocation.
          */
         if
            or
               eq
                  get-count:x:@.arguments/*/args/*/primary/*
                  .:int:0
               eq
                  get-count:x:@.arguments/*/args/*/columns/*
                  .:int:0
            .lambda

               /*
                * Cannot have a PUT endpoint without any [values] to modify and
                * [where] statements to declare which records to modify.
                */
               throw:There are no actual columns, and/or primary keys declared for your PUT endpoint.
                  public:true
                  status:401

         /*
          * Applying PUT arguments.
          * First our primary keys, which ends up becoming a [where] condition,
          * to avoid allowing for updating all records.
          *
          * This is done by making sure all specified primary keys becomes mandatory
          * arguments when invoking the endpoint.
          */
         for-each:x:@.arguments/*/args/*/primary/*/*
            .x:@.arguments/*/
            set-value:x:@.x
               strings.concat
                  get-value:x:@.x
                  get-name:x:@.dp/#
            set-x:x:+/*/*
               convert:x:@.x
                  type:x
            insert-after:x:@apply/*/.type
               .
                  validators.mandatory
         add:x:@apply/*/.arguments
            get-nodes:x:@.arguments/*/args/*/primary/*/*

         /*
          * Then adding the [add] into [values] and [where] invocations to our endpoint.
          * This is done by a little bit of intelligent offsetting using expressions,
          * which we can get away with, since we know that all primary keys are mandatory,
          * and declared first in our [.arguments] node in our endpoint.
          */
         for-each:x:@.arguments/*/args/*/columns/*
            set-name:x:+/*/*
               get-value:x:@.dp/#/*/name
            add:x:@apply/*/.arguments
               .
                  .

         /*
          * First adding the [add] into [where] parts, which are the primary keys.
          */
         .x:@.arguments/*/[0,
         set-value:x:@.x
            strings.concat
               get-value:x:@.x
               get-count:x:@.arguments/*/args/*/primary/*/*
               .:]
         set-x:x:+/*/*/*
            convert:x:@.x
               type:x
         insert-before:x:@apply/**/table/././1
            .
               add:x:./*/data.update/*/where/*/and
                  get-nodes

         /*
          * Then adding the [add] into [values] parts, which are the values to update.
          *
          * Notice, we assume there are less than 1.000 values to update, which should be
          * enough for any sane database table.
          */
         .x:@.arguments/*/[
         set-value:x:@.x
            strings.concat
               get-value:x:@.x
               get-count:x:@.arguments/*/args/*/primary/*/*
               .:,1000]
         set-x:x:+/*/*/*
            convert:x:@.x
               type:x
         insert-before:x:@apply/**/table/././1
            .
               add:x:./*/data.update/*/values
                  get-nodes


         /*
          * Applying foreign keys if specified.
          */
         for-each:x:@.arguments/*/args/*/columns/*

            /*
             * Checking if currently iterated column has a foreign key declaration.
             */
            if
               exists:x:@.dp/#/*/foreign_key/*
               .lambda

                  /*
                   * Adding meta information about foreign key to lambda object.
                   */
                  unwrap:x:+/*/*/*
                  add:x:@apply/*/.foreign-keys
                     .
                        .
                           column:x:@.dp/#/*/name
                           table:x:@.dp/#/*/foreign_key/*/table
                           foreign_column:x:@.dp/#/*/foreign_key/*/column
                           foreign_name:x:@.dp/#/*/foreign_key/*/name

         /*
          * Checking if we have foreign keys at all, and if not, removing entire declaration.
          */
         if
            not
               exists:x:@apply/*/.foreign-keys/*
            .lambda

               /*
                * Removing [.foreign-key] declaration altogether.
                */
               remove-nodes:x:@apply/*/.foreign-keys

   /*
    * Checking if caller wants to apply the CQRS pattern to his endpoint.
    */
   if
      and
         exists:x:@.arguments/*/cqrs
         get-value:x:@.arguments/*/cqrs
      .lambda

         /*
          * Notice, we only apply the CQRS pattern to post, put and delete endpoints.
          */
         switch:x:@.arguments/*/verb

            case:put
            case:post
            case:delete

               /*
                * Applying [sockets.signal] just before returning from endpoint.
                *
                * First creating signal name.
                */
               .signal
               set-value:x:@.signal
                  strings.concat
                     get-value:x:@.arguments/*/moduleName
                     .:.
                     get-value:x:@.arguments/*/moduleUrl
                     .:.
                     get-value:x:@.arguments/*/verb

               /*
                * Adding [sockets.signal] invocation to lambda.
                */
               unwrap:x:+/*/*/sockets.signal
               insert-before:x:@apply/*/data.connect/*/unwrap
                  .
                     add:x:./*/sockets.signal/*/args
                        get-nodes:x:@.arguments/*
                     sockets.signal:x:@.signal
                        args

               /*
                * Applying authorisation requirements for signal invocation.
                */
               switch:x:@.arguments/*/cqrsAuthorisation

                  case:inherited

                     /*
                      * SignalR socket invocation should use same authorisation as endpoint.
                      */
                     insert-before:x:@apply/*/data.connect/*/sockets.signal
                        .
                           add:x:./*/sockets.signal
                              get-nodes:x:@auth.ticket.verify
                           set-name:x:./*/sockets.signal/*/auth.ticket.verify
                              .:roles

                  case:roles

                     /*
                      * SignalR socket invocation should use explicit roles when published.
                      */
                     unwrap:x:+/*/*/roles
                     add:x:@apply/*/data.connect/*/sockets.signal
                        .
                           roles:x:@.arguments/*/cqrsAuthorisationValues

                  case:groups

                     /*
                      * SignalR socket invocation should use explicit roles when published.
                      */
                     unwrap:x:+/*/*/groups
                     add:x:@apply/*/data.connect/*/sockets.signal
                        .
                           groups:x:@.arguments/*/cqrsAuthorisationValues

                  case:users

                     /*
                      * SignalR socket invocation should use explicit roles when published.
                      */
                     unwrap:x:+/*/*/users
                     add:x:@apply/*/data.connect/*/sockets.signal
                        .
                           users:x:@.arguments/*/cqrsAuthorisationValues

               /*
                * Updating the endpoint's description to make sure consumers understands
                * it will publish a SignalR signal.
                */
               set-value:x:@apply/*/.description
                  strings.concat
                     get-value:x:@apply/*/.description
                     .:", and publishes the ["
                     get-value:x:@.signal
                     .:"] web socket SignalR message to users authorised to invoke the endpoint, passing in the arguments specified"

   /*
    * Then creating our folder if necessary.
    *
    * We find the foldername here, by taking the entire filepath, and
    * removing its filename. Then we create the folder.
    */
   .folder:/modules/
   set-value:x:@.folder
      strings.concat
         get-value:x:@.folder
         get-value:x:@.arguments/*/moduleName
         .:/
   strings.split:x:@.filename
      .:/
   remove-nodes:x:-/0/-
   set-value:x:@.folder
      strings.concat
         get-value:x:@.folder
         strings.join:x:@strings.split/*
            .:/
         .:/
   io.folder.create:x:@.folder

   /*
    * Applying the correct description to the file.
    */
   set-value:x:@apply/*/.description
      strings.replace:x:@apply/*/.description
         .:{database}
         get-value:x:@.arguments/*/database
   set-value:x:@apply/*/.description
      strings.replace:x:@apply/*/.description
         .:{table}
         get-value:x:@.arguments/*/table

   /*
    * Transforming lambda to Hyperlambda, and saving it to the correct file, making sure
    * we give the filename the correct HTTP verb, extension, etc.
    */
   lambda2hyper:x:@apply/*
   unwrap:x:+
   .path:x:@.folder
   strings.split:x:@.filename
      .:/
   set-value:x:@.path
      strings.concat
         get-value:x:@.path
         get-value:x:@strings.split/0/-
         .:.
         get-value:x:@.arguments/*/verb
         .:.hl

   /*
    * Substituting illegal characters in our filename, such as spaces, etc.
    */
   strings.replace:x:@.path
      .:' '
      .:_

   /*
    * Throwing an exception if the file exists from before, unless [.argument][overwrite] is true.
    */
   if
      and
         or
            not
               exists:x:@.arguments/*/overwrite
            not
               get-value:x:@.arguments/*/overwrite
         io.file.exists:x:@strings.replace
      .lambda
         throw:Module already exists
            status:int:401
            public:bool:true

   /*
    * Saving file, now transformed according to arguments, database, table, columns etc -
    * And with the correct filename.
    */
   io.file.save:x:@strings.replace
      get-value:x:@lambda2hyper

   /*
    * Calculating how many lines of code we created.
    *
    * This is returned back to the caller after crudification is done.
    */
   get-count:x:@apply/**

   /*
    * Checking if this is an HTTP GET verb, at which point we create TWO endpoints,
    * one to retrieve actual data, and another to retrieve the count of records in table.
    *
    * This is done by taking our existing "get items" lambda structure, and modifying it
    * until it becomes a "get count" endpoint file instead.
    */
   if
      eq
         get-value:x:@.arguments/*/verb
         .:get
      .lambda

         /*
          * First removing [.foreign-keys], [columns], adding [count(*) as count, and then transforming lambda
          * to Hyperlambda, and saving it to the correct file - Which is the same filename as the
          * plain get, except appending "-count" to it.
          */
         remove-nodes:x:@apply/*/.foreign-keys
         remove-nodes:x:@apply/**/columns/*
         remove-nodes:x:@apply/**/table/././*/remove-nodes
         remove-nodes:x:@apply/**/return-nodes
         remove-nodes:x:@apply/**/table/././*/add
         remove-nodes:x:@apply/*/.arguments/*/limit
         remove-nodes:x:@apply/*/.arguments/*/offset
         remove-nodes:x:@apply/*/.arguments/*/order
         remove-nodes:x:@apply/*/.arguments/*/direction
         set-value:x:@apply/*/.type
            .:crud-count
         set-value:x:@apply/*/.description
            strings.replace:x:@apply/*/.description
               .:Returns
               .:Counts
         add:x:@apply/**/columns
            .
               count(*)
                  as:count
         add:x:@apply/**/table/.
            .
               limit:long:1
         insert-before:x:@apply/**/table/.
            .
               add:x:+/+/+
                  get-nodes:x:@.arguments/*/operator
               remove-nodes:x:@.arguments/*/operator
               add:x:+/*/where/*
                  get-nodes:x:@.arguments/*
         insert-after:x:@apply/**/table/.
            .
               return-nodes:x:@data.read/*/*
         lambda2hyper:x:@apply/*
         unwrap:x:+
         .path:x:@.folder
         strings.split:x:@.filename
            .:/
         set-value:x:@.path
            strings.concat
               get-value:x:@.path
               get-value:x:@strings.split/0/-
               .:-count
               .:.
               get-value:x:@.arguments/*/verb
               .:.hl

         /*
          * Figuring out filename for HTTP endpoint.
          */
         strings.replace:x:@.path
            .:' '
            .:_

         /*
          * Making sure file does not exist from before, unless [.argument][overwrite] is true.
          */
         if
            and
               or
                  not
                     exists:x:@.arguments/*/overwrite
                  not
                     get-value:x:@.arguments/*/overwrite
               io.file.exists:x:@strings.replace
            .lambda
               throw:Module already exists
                  status:int:401
                  public:bool:true

         /*
          * Saving file, now applied according to template declaration,
          * and with the correct filename.
          */
         io.file.save:x:@strings.replace
            get-value:x:@lambda2hyper

         /*
          * Adding count endpoint LOC count to our above LOC value.
          */
         set-value:x:@get-count
            math.add
               get-value:x:@get-count
               get-count:x:@apply/**

   /*
    * Making sure we evict the cache entry for endpoints.
    */
   cache.set:endpoints

   /*
    * Returning success to caller.
    */
   unwrap:x:+/*
   return
      result:success
      loc:x:@get-count
