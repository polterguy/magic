
/*
 * Executes the specified [macro] file passing in the specified [args].
 */
.description:Executes the specified [macro] file passing in the specified [args]
.arguments
   macro:string
   args:*

/*
 * Making sure user is authorised to access endpoint.
 */
auth.ticket.verify:root

/*
 * Sanity checking invocation.
 */
validators.mandatory:x:@.arguments/*/macro

/*
 * Loading macro Hyperlambda file and transforming to lambda.
 */
io.file.load:x:@.arguments/*/macro
hyper2lambda:x:@io.file.load

/*
 * Decorating macro's [.lambda] object.
 */
add:x:+/*/*
   get-nodes:x:@.arguments/*/args/*
insert-before:x:@hyper2lambda/*/.lambda/0
   .
      .arguments

/*
 * Executing macro.
 */
eval:x:@hyper2lambda/*/.lambda

/*
 * Returning success to caller.
 */
unwrap:x:+/*
return
   result:x:@hyper2lambda/*/.lambda
