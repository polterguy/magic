
/*
 * Manually created assumption that verifies we can authenticate towards 'self'
 * using a crypto challenge.
 */
description:Assumes that we can authenticate using our private key and a crypto challenge.
.lambda

   // Notice, we only execute this test on localhost.
   request.host
   if
      not
         strings.starts-with:x:@request.host
            .:localhost
      .lambda
         return:success

   // Common URL for all invocations.
   .url
   set-value:x:@.url
      strings.concat
         request.scheme
         .:"://"
         request.host
         .:/magic/system/crypto/challenge

   // Invoking server to retrieve a cryptography challenge.
   http.get:x:@.url

   // Loading public key needed to supply fingerprint to signing process.
   io.file.load:/etc/system/crypto/keys/public.key
   crypto.fingerprint:x:-

   // Loading private key needed to cryptographically sign challenge.
   io.file.load:/etc/system/crypto/keys/private.key

   // Cryptographically signing challenge.
   json2lambda:x:@http.get/*/content
   crypto.sign:x:@json2lambda/*/result
      signing-key:x:@io.file.load
      signing-key-fingerprint:x:@crypto.fingerprint

   // Posting cryptographically signed challenge to server.
   unwrap:x:+/*
   lambda2json
      challenge:x:@crypto.sign
   http.post:x:@.url
      payload:x:@lambda2json

   /*
    * Retrieving JWT token from result returned from server,
    * and verifying server returned a valid token, for then to invoke endpoint
    * to execute some Hyperlambda requiring us to be authenticated as root.
    */
   json2lambda:x:@http.post/*/content
   .url
   set-value:x:@.url
      strings.concat
         request.scheme
         .:"://"
         request.host
         .:/magic/system/evaluator/evaluate
   http.post:x:@.url
      token:x:@json2lambda/*/result
      payload:"{\"hyperlambda\":\"auth.ticket.verify:root\"}"

   // Verifying invocation succeeded.
   if
      not
         eq
            get-value:x:@http.post
            .:int:200
      .lambda
         throw:Assumption failed, [http.post] invocation with ticket returned from challenge did not return success
