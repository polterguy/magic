
// Macro for Hyper IDE that creates an upload image endpoint.
.name:Creates a download file endpoint
.description:"Macro for Hyper IDE that creates a 'download file' HTTP GET endpoint for you where the file is specified as an [file] query parameter. This macro goes hand in hand with the upload file macro, and becomes its 'get' equivalent."
.arguments
   .
      name:module
      type:string
      description:"Name of module to create a 'download file endpoint' for"
      mandatory:bool:true
   .
      name:auth
      type:string
      description:Comma separated list of roles users must belong to in order to invoke endpoint. If empty, there are no authorisation requirements
      mandatory:bool:false
   .
      name:name
      type:string
      description:Name of your endpoint that becomes the last parts of its URL
      mandatory:bool:true
      default:download-file

// Lambda object executed as macro is actually executed.
.lambda

   // Sanity checking invocation.
   validators.mandatory:x:@.arguments/*/module
   validators.regex:x:@.arguments/*/module
      regex:^[a-zA-Z0-9_-]+$
   validators.mandatory:x:@.arguments/*/name
   validators.regex:x:@.arguments/*/name
      regex:^[a-zA-Z0-9_-]+$

   // Figuring out module's complete path.
   .module-path
   set-value:x:@.module-path
      strings.concat
         .:/modules/
         get-value:x:@.arguments/*/module
         .:/

   // Making sure that module exists.
   if
      not
         io.folder.exists:x:@.module-path
      .lambda

         // No such module
         strings.concat
            .:"The module called '"
            get-value:x:@.arguments/*/module
            .:"' does not exist"
         throw:x:-
            public:bool:true
            status:int:404

   // Making sure that module does not already have a 'xxx.get.hl' file.
   strings.concat
      get-value:x:@.module-path
      get-value:x:@.arguments/*/name
      .:.get.hl
   if
      io.file.exists:x:@strings.concat
      .lambda

         // File already exists.
         strings.concat
            .:"The module called '"
            get-value:x:@.arguments/*/module
            .:"' already has a '"
            get-value:x:@.arguments/*/name
            .:".get.hl' file and hence we cannot create another"
         throw:x:-
            public:bool:true
            status:int:404

   // Copying main 'download-image.get.hl' template file from '/misc/idea/templates' folder.
   io.file.copy:/misc/ide/templates/download-file.get.hl
      get-value:x:@strings.concat

   /*
    * Opening 'xxx.get.hl' file such that we can correctly decorate it
    * according to arguments specified by caller.
    */
   strings.concat
      get-value:x:@.module-path
      get-value:x:@.arguments/*/name
      .:.get.hl
   io.file.load:x:@strings.concat
   hyper2lambda:x:@io.file.load
      comments:true

   // Substituting parameters.
   if
      or
         eq
            get-value:x:@.arguments/*/auth
            .
         eq
            get-value:x:@.arguments/*/auth
            .:
         not
            exists:x:@.arguments/*/auth
      .lambda

         // No [auth] specified.
         remove-nodes:x:@hyper2lambda/**/=[[auth]]
   else

      // Auth specified.
      set-value:x:@hyper2lambda/**/=[[auth]]
         get-value:x:@.arguments/*/auth
   set-value:x:@hyper2lambda/**/=[[module]]
      get-value:x:@.arguments/*/module

   // Then transforming file to Hyperlambda and saving it again.
   lambda2hyper:x:@hyper2lambda/*
      comments:true
   io.file.save:x:@strings.concat
      get-value:x:@lambda2hyper

   // Making sure we inform caller that he needs to refresh his folders.
   set-value:x:@.lambda
      strings.concat
         .:folders-changed|
         get-value:x:@.module-path
